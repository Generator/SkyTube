apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "free.rm.skytube"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 9
        buildConfigField 'String[]', 'YOUTUBE_API_KEYS',
                '{' +
                        '"AIzaSyBK7NLymFzs-P8j8jT7kjeWkY0yXdue8_g",' +
                        '"AIzaSyC23oUYMmy2hlmxSJNvo5UyfvmNmAJ_Lao",' +
                        '"AIzaSyA-Y12I5tlGUcodUyH-RQHn94F7c60Y4d0"'  +
                '}';
        buildConfigField 'String', 'SKYTUBE_WEBSITE',         '"http://skytube-app.com"'
        buildConfigField 'String', 'SKYTUBE_WEBSITE_CREDITS', '"http://skytube-app.com/credits.html"'
        buildConfigField 'String', 'SKYTUBE_UPDATES_URL',     '"https://api.github.com/repos/ram-on/SkyTube/releases/latest"'
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true    // this is as we have over 65536 lines of code...
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        // 100% Open-Source Edition
        oss {
            applicationId "free.rm.skytube.oss"
            versionName "2.60"          // the digits of the *fractional part* shall not be larger than "9"
            resValue "string", "app_name", "SkyTube"
        }
        // 99% Open-source edition:  uses the YouTube Player Jar [i.e. proprietary module - optional usage]
        extra {
            applicationId "free.rm.skytube.extra"
            versionName "2.60 Extra"    // the digits of the *fractional part* shall not be larger than "9"
            resValue "string", "app_name", "SkyTube Extra"
        }
    }
}

configurations {
    nonFreeCompile
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    nonFreeCompile files('libs/YouTubeAndroidPlayerApi.jar')

    compile group: 'org.ocpsoft.prettytime', name: 'prettytime', version: '3.2.7.Final'
    compile 'com.google.http-client:google-http-client-android:1.22.0'
    compile 'com.google.apis:google-api-services-youtube:v3-rev181-1.22.0'
    compile 'com.google.android.exoplayer:exoplayer:r2.4.3'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'

    // libextractor
    compile 'org.jsoup:jsoup:1.8.3'
    compile 'org.mozilla:rhino:1.7.7'

    compile 'com.github.moraisigor:slidingdrawer:1.5.5'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.google.code.gson:gson:2.7'
    compile group: 'joda-time', name: 'joda-time', version: '2.3'
    compile 'com.afollestad.material-dialogs:core:0.9.0.2'
    compile 'com.github.angads25:filepicker:1.1.1'

    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:support-v13:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:multidex:1.0.1'    // this is as we have over 65536 lines of code...
}
